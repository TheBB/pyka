@@grammar :: Kaleidoscope

@@eol_comments :: /;.*?$/

toplevel = @:(extern | definition | expr) $;

extern = 'extern' @:prototype;
definition = 'def' @+:prototype @+:expr;

prototype = @+:symbol '(' @+:param_list ')';
param_list = ','.{ symbol };

expr = arith;
arith = term {('+' | '-') term};
term = factor {('*' | '/') factor};
factor = atom_expr;

atom_expr = trailer_expr;
trailer_expr = atom { trailer };
trailer = '(' arg_list ')';

arg_list = ','.{ expr };

atom = '(' @:expr ')' | symbol | number;
symbol = /[a-zA-Z_][a-zA-Z0-9_]*/;
number = /[0-9]*([0-9]\.|\.[0-9]|[0-9])[0-9]*([eE][+-]?[0-9]+)?/;
